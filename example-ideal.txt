  LOAD(EMPTY)

  /////////////////////////////////////////////////////////
  // State 0: go West
  /////////////////////////////////////////////////////////

  // as long as West is unblocked, go West  
  STATE 0 TO 0
  IF (Anything, Anything, Open, Anything) THEN West

  // can't go West anymore, so try to go North (by transitioning to State 1)
  STATE 0 TO 1 
  IF (Anything, Anything, Blocked, Anything) THEN Wait


  /////////////////////////////////////////////////////////
  // State 1: go North
  /////////////////////////////////////////////////////////

  // as long as North is unblocked, go North
  STATE 1 TO 1 
  IF (Open, Anything, Anything, Anything) THEN North

  // can't go North any more, so try to go South (by transitioning to State 2)
  STATE 1 TO 2
  IF (Blocked, Anything, Anything, Open) THEN South


  /////////////////////////////////////////////////////////
  // States 2 & 3: fill from North to South, West to East
  /////////////////////////////////////////////////////////

  // State 2: fill this column from North to South
  STATE 2 TO 2 
  IF (Anything, Anything, Anything, Open) THEN South

  // can't go South anymore, move one column to the East and go North
  // (by transitioning to State 3)
  STATE 2 TO 3 
  IF (Anything, Open, Anything, Blocked) THEN East

  // State 3: fill this column from South to North
  STATE 3 TO 3 
  IF (Open, Anything, Anything, Anything) THEN North

  // can't go North anymore, move one column to the East and go South
  // (by transitioning to State 2)
  STATE 3 TO 2 
  IF (Blocked, Open, Anything, Anything) THEN East

  GO